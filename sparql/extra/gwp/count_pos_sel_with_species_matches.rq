# Count all sequences under positive selection that match
# against species
#
# ./scripts/sparql.sh sparql/extra/gwp/count_pos_sel_with_species_matches.rq |grep Mi_CDS_cluster00159|wc # should be 48
# 
# Expect: | Mi | CDS | Mi | DNA | Mi_CDS_cluster00159_Minc_Contig9_302> (46x)
#
# | Ts       | DNA     | Ce        | CDS      | <http://biobeat.org/rdf/ns#Ts_DNA_cluster00035> | T21G5.5a                         |
# | Ts       | DNA     | Ce        | CDS      | <http://biobeat.org/rdf/ns#Ts_DNA_cluster00035> | T21G5.5b                         |
# | Ts       | DNA     | Ce        | CDS      | <http://biobeat.org/rdf/ns#Ts_DNA_cluster00035> | T21G5.5d                         |
# | Ts       | DNA     | Mh        | CDS      | <http://biobeat.org/rdf/ns#Ts_DNA_cluster00256> | MhA1_Contig1146.frz3.gene3       |
# | Ts       | DNA     | Mh        | CDS      | <http://biobeat.org/rdf/ns#Ts_DNA_cluster00256> | MhA1_Contig163.frz3.gene5        |
# | Ts       | DNA     | Mh        | CDS      | <http://biobeat.org/rdf/ns#Ts_DNA_cluster00256> | MhA1_Contig2314.frz3.gene2       |
# 
# .---------------------------------------------------.
# | ?species | ?source | ?hspecies | ?hsource | ?fams |
# |----------+---------+-----------+----------+-------|
# | Mh       | DNA     | Mi        | DNA      | 13    |
# | Mi       | DNA     | Mh        | DNA      | 5     |
# | Mi       | DNA     | Mi        | CDS      | 11    |
# | Pp       | CDS     | Gp        | DNA      | 14    |
# '---------------------------------------------------'

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix hgnc: <http://identifiers.org/hgnc.symbol/>
prefix doi: <http://dx.doi.org/>
prefix : <http://biobeat.org/rdf/ns#> 

SELECT ?species ?source ?hspecies ?hsource ((COUNT(?fam)) AS ?fams) WHERE
# SELECT ?species ?source ?hspecies ?hsource ?cluster ?hgene WHERE 
{
  {
    # Find all homolog sequences in cluster
    # ?seq :cluster :Mi_CDS_cluster00159 .
    ?seq a :blast_match .
    ?seq :homolog_species ?hspecies .
    ?seq :homolog_source ?hsource .
    ?seq :homolog_gene ?hgene .
    ?seq :cluster ?cluster .
    # Find all families and species under pos sel 
    ?fam :clusterid ?cluster .
    ?fam :species ?species .
    ?fam :source ?source .
    ?fam :is_pos_sel true .
  }
  FILTER (?hsource != ?source || ?species != ?hspecies) .
}
GROUP by ?species ?source ?hspecies ?hsource 
# ORDER by ?species 
# ORDER by ?species ?source ?hspecies ?hsource 
